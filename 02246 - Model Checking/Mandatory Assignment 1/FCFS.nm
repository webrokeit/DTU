mdp

module client1
  state1 : [0..1] init 0; // State of the job (inactive/active)
  task1  : [0..5] init 0; // Length of the job
  
  // Create a new job - length chose non-deterministically
  [create1] state1=0 -> (state1'=1) & (task1'=1);
  [create1] state1=0 -> (state1'=1) & (task1'=2);
  [create1] state1=0 -> (state1'=1) & (task1'=3);
  [create1] state1=0 -> (state1'=1) & (task1'=4);
  [create1] state1=0 -> (state1'=1) & (task1'=5);

  // Serve the job
  [serve1] state1=1 & task1>0 -> (task1'=task1-1);

  // Complete the job
  [finish1] state1=1 & task1=0 -> (state1'=0);

endmodule

module client2 = client1 [state1=state2,
                          task1=task2,
                          create1=create2,
                          serve1=serve2,
                          finish1=finish2 ]
endmodule

module client3 = client1 [state1=state3,
			  task1=task3,
			  create1=create3,
			  serve1=serve3,
			  finish1=finish3 ]

endmodule

module scheduler
  job1 : [0..3] init 0; // First job in the queue
  job2 : [0..3] init 0; // Second job in the queue
  job3 : [0..3] init 0; // Third job in the queue

  // Place a new job at the end of the queue
  [create1] job3=0 -> (job3'=1);
  [create2] job3=0 -> (job3'=2);
  [create3] job3=0 -> (job3'=3);

  // Shift the queue if there is an empty slot
  [] job2=0 & job3>0 -> (job2'=job3) & (job3'=0);
  [] job1=0 & job2>0 -> (job1'=job2) & (job2'=0);

  // Serve the job at the head of the queue
  [serve1] job1=1 -> true;
  [serve2] job1=2 -> true;
  [serve3] job1=3 -> true;

  // Complete the job at the head of the queue
  [finish1] job1=1 -> (job1'=0);
  [finish2] job1=2 -> (job1'=0);
  [finish3] job1=3 -> (job1'=0);

endmodule

system
  scheduler || client1 || client2 || client3
endsystem

